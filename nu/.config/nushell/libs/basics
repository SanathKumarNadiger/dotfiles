# vim: set filetype=nu:

export def wait-for [host: string] {
    let syms = [ '/' '|' '\' '-' ]
    mut i = 0
    loop {
        let resp = (do -s {ping -c 1 -W 1 $"($host)" -q }| complete)
        if $resp.exit_code == 1 {
            print -n $"[($syms | get ($i mod 4))] Waiting for ($host)\r"
            $i = $i + 1
            continue
        }
        if $resp.exit_code == 2 {
            print $"($resp.stderr)"
            return
        }
        break
    }
    (notify-send -t 5000 $"($host)" "up" | complete )| null
    do -i {mpv /usr/share/sounds/freedesktop/stereo/complete.oga} | complete | null
}

export def mr [] {
    let mpd_dir = $"($env.HOME)/storage/music"
    #--------------------
    let current_song = ([$mpd_dir,(mpc current -f "%file%")] | path join )
    mv $current_song $"($env.HOME)/tmp"
    mpc rescan
    mpc next
    notify-send "mpd" $"'($current_song | path basename)' moved to ~/tmp" -t 5000
}
